#lang alacrity/exe
(require "stdlib.ala")

(define-values (ROCK PAPER SCISSORS) (values 0 1 2))
(define (hand? x)
  (or (= x ROCK) (or (= x PAPER) (= x SCISSORS))))

(define-values (B_WINS DRAW A_WINS) (values 0 1 2))
(define (outcome? x)
  (or (= x B_WINS) (or (= x DRAW) (= x A_WINS))))

(define (RPS-outcome A-hand B-hand) : outcome?
  (define A-valid? (hand? A-hand))
  (define B-valid? (hand? B-hand))
  (cond [(and A-valid? B-valid?)
         (modulo (+ A-hand (- 4 B-hand)) 3)]
        [A-valid? A_WINS]
        [B-valid? B_WINS]
        [else DRAW]))

#:participants
(define-participant A
  [Swager-amount : int]
  [Sescrow-amount : int]
  [SA-hand : int])
(define-participant B
  [SB-hand : int])

#:main
(@ A (assert! (hand? SA-hand)))
(@ A (define-values (SA-commit SA-salt) (precommit SA-hand)))
(@ A (define-values (wager-amount escrow-amount A-commit)
       (values (declassify Swager-amount)
               (declassify Sescrow-amount)
               (declassify SA-commit))))
(@ A (publish! wager-amount escrow-amount A-commit)
     (pay! (+ wager-amount escrow-amount)))

(@ B (assert! (hand? SB-hand)))
(@ B (define B-hand (declassify SB-hand)))
(@ B (publish! B-hand) (pay! wager-amount)
     (assert! (implies (HONEST) (hand? B-hand))))

(@ A (define A-salt (declassify SA-salt)))
(@ A (define A-hand (declassify SA-hand)))
(@ A (publish! A-salt A-hand) (pay! 0)
     (check-commit! A-commit A-salt A-hand)
     (assert! (implies (HONEST) (hand? A-hand)))
     (define outcome (RPS-outcome A-hand B-hand))
     (assert! (implies (= outcome A_WINS) (hand? A-hand)))
     (assert! (implies (= outcome B_WINS) (hand? B-hand)))
     (define-values (A-gets B-gets)
       (cond
         [(= outcome A_WINS)
          (values (+ (* 2 wager-amount) escrow-amount) 0)]
         [(= outcome B_WINS)
          (values escrow-amount (* 2 wager-amount))]
         [else
          (values (+ wager-amount escrow-amount) wager-amount)]))
     (transfer! A A-gets)
     (transfer! B B-gets))

outcome
