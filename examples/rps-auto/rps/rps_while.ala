#lang alacrity/exe

enum isHand { ROCK, PAPER, SCISSORS };

function getHand() : isHand {
    const s = interact("getHand");
    assume! isHand(s);
    s
}

enum isOutcome { B_WINS, DRAW, A_WINS };

function winner(handA, handB) : isOutcome {
    assert! isHand(handA);
    assert! isHand(handB);
    ((handA + (4 - handB)) % 3) }

function fair_if(handX, optionX, canWinX) {
    possible? ((handX == optionX) && canWinX) }

function fair_for_player(handX, canWinX) {
    fair_if(handX, ROCK, canWinX);
    fair_if(handX, PAPER, canWinX);
    fair_if(handX, SCISSORS, canWinX) }

function fair_game(handA, handB, outcome) {
    fair_for_player(handA, (outcome == A_WINS));
    fair_for_player(handB, (outcome == B_WINS)) }

participant A {
    uint256 wagerAmount,
    uint256 escrowAmount }

participant B { }

main {
    @A publish! wagerAmount, escrowAmount
       w/ (wagerAmount + escrowAmount);
    commit;

    @B pay! wagerAmount;

    do const outcome = DRAW // TODO: if this is going to be re-bound, make it a let instead of a const???
       until not((outcome == DRAW))
       invariant ((balance() == ((2 * wagerAmount) + escrowAmount))
               && isOutcome(outcome))
    {
        commit;

        @A const handA = getHand();
        @A const commitA, saltA = precommit(handA);
        @A interact("commits");
        @A publish! commitA w/ 0;
        commit;

        @B const handB = getHand();
        @B interact("shows");
        @B publish! handB w/ 0;
        require! isHand(handB);
        commit;

        @A interact("reveals");
        @A publish! saltA, handA w/ 0;
        check_commit(commitA, saltA, handA);
        require! isHand(handA);
        const this_outcome = winner(handA, handB);
        fair_game(handA, handB, this_outcome);

        continue this_outcome
    };

    assert! not((outcome == DRAW));
    const getsA, getsB =
          if (outcome == A_WINS) {
              values (2 * wagerAmount), 0 }
          else {
              values 0, (2 * wagerAmount) };
    transfer! A <- (escrowAmount + getsA);
    transfer! B <- getsB;
    commit;

    outcome }
